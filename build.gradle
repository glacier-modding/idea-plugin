String properties(String key) {
    return project.findProperty(key).toString()
}

plugins {
    id "groovy"
    id "org.jetbrains.intellij" version "1.5.3"
}

group = properties("pluginGroup")
version = properties("pluginVersion")

// Configure project's dependencies
repositories {
    mavenCentral()
}

dependencies {
}

// Configure Gradle IntelliJ Plugin - read more: https://github.com/JetBrains/gradle-intellij-plugin
intellij {
    pluginName = properties("pluginName")
    version = properties("platformVersion")
    type = properties("platformType")

    plugins = ("true" == properties("enableJs")) ? ["JavaScript"] : []
}

String javaVersion = properties("javaVersion")

tasks.withType(JavaCompile) {
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
}

wrapper {
    gradleVersion = properties("gradleVersion")
}

patchPluginXml {
    version = properties("pluginVersion")
}

signPlugin {
    certificateChain = System.getenv("CERTIFICATE_CHAIN")
    privateKey = System.getenv("PRIVATE_KEY")
    password = System.getenv("PRIVATE_KEY_PASSWORD")
}

publishPlugin {
    token = System.getenv("PUBLISH_TOKEN")
    channels = Arrays.asList("default")
}

jar {
    duplicatesStrategy = DuplicatesStrategy.WARN
}

tasks.withType(AbstractArchiveTask) {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}
